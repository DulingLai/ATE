/*
 * Copyright (C) 2015 The Pennsylvania State University and the University of Wisconsin
 * Systems and Internet Infrastructure Security Laboratory
 *
 * Author: Damien Octeau
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


class android.content.IntentFilter {
  Set<String> actions;
  Set<String> categories;

  copy <android.content.IntentFilter: void <init>(android.content.IntentFilter)> {
    0;
  }
  argument replaceActions = 0: replaceAll actions;
  mod <android.content.IntentFilter: void <init>(java.lang.String)> {
    argument replaceActions;
  }
  mod <android.content.IntentFilter: void addAction(java.lang.String)> {
    0: add actions;
  }
  mod <android.content.IntentFilter: void addCategory(java.lang.String)> {
    0: add categories;
  }
  mod gen <android.content.IntentFilter: android.content.IntentFilter create(java.lang.String,java.lang.String)> {
    0: add actions;
  }
  super context = android.content.Context;
  argument type0 = 0: type classType, prop "valueType": "receiverType";
  argument intent1 = 1: type android.content.IntentFilter, prop "valueType": "intentFilter";
  query context : android.content.Intent registerReceiver(android.content.BroadcastReceiver,android.content.IntentFilter) {
    argument type0;
    argument intent1;
  }
  query context : android.content.Intent registerReceiver(android.content.BroadcastReceiver,android.content.IntentFilter,java.lang.String,android.os.Handler) {
    argument type0;
    argument intent1;
    2: type String, prop "valueType": "permission";
  }
}
